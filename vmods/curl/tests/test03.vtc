varnishtest "Test objects are cleared out on get when restarting"

server s1 {
	rxreq
	txresp -hdr "Foo: bar"
	rxreq
	txresp -hdr "Foo: bar"
} -start

varnish v1 -vcl+backend {
	import curl from "${builddir}/libvmod_curl.so";
	sub vcl_deliver {
		curl.get("http://localhost:65500");
		if (req.restarts < 1) {
			return (restart);
		}
		set resp.http.x-error = curl.error();

		# Fix for RHEL5 old curl lib
		if (resp.http.x-error == "couldn't connect to server") {
			set resp.http.x-error = "Couldn't connect to server";
		}
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.status == 200
	expect resp.http.x-error == "Couldn't connect to server"
}

client c1 -run
client c1 -run
